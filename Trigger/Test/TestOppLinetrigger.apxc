@isTest
public class TestOppLinetrigger {
    // --------------------------- HELPER METHOD ---------------------------------------//
    @testSetup
    public static void loadData(){
        // Insert Price Book
        Pricebook2 customPricebook = new Pricebook2(
            Name = 'My CustomTestBook',
            IsActive = true
        );
        Insert customPricebook;
        
        // Insert Product
        List<Product2> prodList = new List<Product2>();
        for(Integer i = 1; i <= 3; i++){
            Product2 prod = new Product2(Name = 'Test Product '+ i, ProductCode = 'P00'+i, IsActive = true);
            prodList.add(prod);
        }
        Insert prodList;
        //system.debug('prodList : ' + prodList);
        
        // Create Standard Pricebook Entry
        List<PricebookEntry> priceEntries = new List<PricebookEntry>();
        for(Product2 prod : prodList) {
            PricebookEntry obj = new PricebookEntry(
                Pricebook2Id = Test.getStandardPricebookId(),
                Product2Id = prod.Id,
                UnitPrice = 1020,
                IsActive = true
            );
            priceEntries.add(obj);  
        }
        Insert priceEntries;
        //system.debug('priceEntries : ' + priceEntries);
        
        // Create Custom Pricebook Entry
        List<PricebookEntry> customPriceEntries = new List<PricebookEntry>();
        for(Product2 prod : prodList) {
            PricebookEntry obj = new PricebookEntry(
                Pricebook2Id = customPricebook.Id,
                Product2Id = prod.Id,
                UnitPrice = 1020,
                IsActive = true
            );
            customPriceEntries.add(obj);  
        }
        Insert customPriceEntries;
        //system.debug('customPriceEntries :' + customPriceEntries);
        
        // Create Opportunities
        List<Opportunity> oppList = new List<Opportunity>();
        for (Integer i = 0; i < 2; i++) {
            oppList.add(new Opportunity(
                Name = 'BulkOpp' +' ' + i,
                StageName = 'Prospecting',
                CloseDate = Date.today()
            ));
        }
        Insert oppList;
        
        // Insert Opportunity Line Items
        List<OpportunityLineItem> oliList = new List<OpportunityLineItem>();
        for (Opportunity opp : oppList) {
            for (PricebookEntry prod : customPriceEntries) {
                oliList.add(new OpportunityLineItem(
                    OpportunityId = opp.Id,
                    Quantity = 1,
                    UnitPrice = 100,
                    PricebookEntryId = prod.Id
                ));
            }
        }
        Insert oliList;
       // system.debug(oliList);
       
    }
    @isTest
    public static void testInsertOLIs(){
        List<Opportunity> oppList = [SELECT Id, OppLineItem_Code__c 
                                     FROM Opportunity 
                                     WHERE Name LIKE 'BulkOpp%' LIMIT 1] ;
        String OppLineCode;
        if(!oppList.isEmpty()){
            OppLineCode = oppList[0].OppLineItem_Code__c;
        }
        Assert.areEqual('P001, P002, P003', OppLineCode, 'Product code did not match');
    }
    
    // ---------------------------BULK TEST-DELETE-SINGLE OLI --------------------------------------//
    @isTest
    public static void testDeleteOLIs() {
        // Fetch Opp
        List<Opportunity> oppList = [SELECT Id, OppLineItem_Code__c 
                                     FROM Opportunity 
                                     WHERE Name LIKE 'BulkOpp%' LIMIT 1] ; 
        
        // Fetch All OLI
        List<OpportunityLineItem> oliToDelete = [SELECT Id FROM OpportunityLineItem 
                                                 WHERE OpportunityId = :oppList[0].Id];
        if (!oliToDelete.isEmpty()) {
            delete oliToDelete[0];
        }
        
        // Fetch Active OLI
        List<AggregateResult> remainingOliCounts = [SELECT Count(Id) OliCode from OpportunityLineItem 
                                                    Where OpportunityId = :oppList[0].Id];
        
        Assert.areEqual(oliToDelete.size() - 1 , remainingOliCounts[0].get('OliCode'), 'OLIs count did not match after deletion');
    }
    
    // ---------------------------BULK TEST-DELETE-ALL OLI --------------------------------------//
    @isTest
    public static void testDeleteAllOLIs() {
        // Fetch Opp
        List<Opportunity> oppList = [SELECT Id, OppLineItem_Code__c 
                                     FROM Opportunity 
                                     WHERE Name LIKE 'BulkOpp%' LIMIT 1] ; 
        
        List<OpportunityLineItem> oliToDelete = [SELECT Id FROM OpportunityLineItem 
                                                 WHERE OpportunityId = :oppList[0].Id];
        if (!oliToDelete.isEmpty()) {
            delete oliToDelete;
        }
        
        // Fetch OLI after Delete
        List<OpportunityLineItem> oliAfterDelete = [SELECT Id FROM OpportunityLineItem 
                                                    WHERE OpportunityId = :oppList[0].Id];
        
        // Fetch Opp after OLI Delete
        List<Opportunity> updatedOppList = [SELECT Id, OppLineItem_Code__c 
                                            FROM Opportunity 
                                            WHERE Name LIKE 'BulkOpp%' LIMIT 1];
        String OppLineCode;
        if(!updatedOppList.isEmpty()){
            OppLineCode = updatedOppList[0].OppLineItem_Code__c;
        }
        
        Assert.areEqual(0, oliAfterDelete.size(), 'OLIs count did not match after deletion');
        Assert.areEqual(Null, OppLineCode, 'No product code should be present after all OLI deleted');
    }
}
